plugins {
    id 'java'
}

def mcVersion = paperApiVersion.substring(0, paperApiVersion.indexOf("-"))
def mcVersionParts = mcVersion.split("\\.")
def protocolLibVersion = (
    mcVersionParts.length > 2 &&
    mcVersionParts[1] == "21" &&
    Integer.parseInt(mcVersionParts[2]) >= 3
) ? '5.4.0': '5.3.0';

group 'in.thekreml'
version "${pluginVersion}-${mcVersion}"

repositories {
    mavenCentral()
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:${paperApiVersion}"
    compileOnly "com.github.MilkBowl:VaultAPI:${vaultApiVersion}"
    compileOnly "net.dmulloy2:ProtocolLib:${protocolLibVersion}"
}

processResources {
    filter {
        String line -> line
                .replaceAll("\\{pluginVersion}", pluginVersion)
                .replaceAll("\\{apiVersion}", mcVersion)
    }
}